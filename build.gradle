import dev.ithundxr.silk.ChangelogText

plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.4-SNAPSHOT" apply false
    id "me.modmuss50.mod-publish-plugin" version "0.3.5"
    id "io.github.pacifistmc.forgix" version "1.2.6"
    id "dev.ithundxr.silk" version "0.11.15"
}

architectury {
    minecraft = minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        mappings loom.officialMojangMappings()
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = archives_base_name
    version = mod_version
    group = maven_group

    repositories {}

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

forgix {
    group = maven_group
    mergedJarName = "$archives_base_name-$mod_version+mc$minecraft_version" + ".jar"
    outputDir = "build/libs/Merged"

    forge {
        projectName = "neoforge"
        jarLocation = "build/libs/$archives_base_name-$mod_version" + ".jar"
    }

    fabric {
        projectName = "fabric"
        jarLocation = "build/libs/$archives_base_name-$mod_version" + ".jar"
    }
}

publishMods {
    displayName = "Old MC Logo v$mod_version for mc$minecraft_version"
    file = file("build/libs/Merged/$archives_base_name-$mod_version+mc$minecraft_version" + ".jar")
    changelog = ChangelogText.getChangelogText(project)
    type = STABLE
    modLoaders.add("fabric")
    modLoaders.add("quilt")
    modLoaders.add("neoforge")

    curseforge {
        projectId = curseforge_id
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        minecraftVersions.add(minecraft_version)
    }

    modrinth {
        projectId = modrinth_id
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        minecraftVersions.add(minecraft_version)
    }
}